plugins {
    // to check for updates run: ./gradlew dependencyUpdates -Drevision=release
    id 'com.github.ben-manes.versions' version '0.50.0'
}

ext {
    currentVersion = '0.9.3-SNAPSHOT'
    lastRelease = '0.9.2'
    junitVersion = '5.12.0'
    junitPlatformVersion = '1.9.2'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    // java-library will force inter-project dependencies to use class files instead of jars
    // This system property disables that and goes back to using jars
    System.setProperty("org.gradle.java.compile-classpath-packaging", "true");
    
    ext.publishScript = rootProject.rootDir.absolutePath + '/publish.gradle'

    group = 'org.microshed'
    version = currentVersion
    
    sourceCompatibility = 11
    targetCompatibility = 11
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    javadoc {
        exclude '**/internal/**'
    }

    test {
        defaultCharacterEncoding = "UTF-8"
        useJUnitPlatform()
        testLogging {
            displayGranularity 1
            showStandardStreams = true
            showStackTraces = true
            exceptionFormat = 'full'
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
    }

    dependencies {
        constraints {
            implementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
            implementation("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
            implementation("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
            implementation("org.junit.platform:junit-platform-engine:${junitPlatformVersion}")
        }
    }
}

task updateVersion {
    doLast {
        if (!project.hasProperty('nextVersion'))
            throw GradleException("Must define '-PnextVersion=X.Y.Z' when running this task")
        def isRelease = !nextVersion.endsWith('SNAPSHOT')

        println 'Updating project version: ' + currentVersion + ' --> ' + nextVersion
        ant.replaceregexp(match: '(["\'>])' + currentVersion + '(["\'<])',
                        replace: '\\1' + nextVersion + '\\2', flags: 'g', byline: true) {
            fileset(dir: '.', includes: '**/*.gradle')
            fileset(dir: '.', includes: '**/pom.xml')
        }

        if (isRelease) {
            println 'Updating doc version: ' + lastRelease + ' --> ' + nextVersion
            ant.replaceregexp(match: '(["\'>])' + lastRelease + '(["\'<])',
                            replace: '\\1' + nextVersion + '\\2', flags: 'g', byline: true) {
                fileset(dir: '.', includes: '**/*.md')
                fileset(dir: '.', includes: 'build.gradle')
            }
        } else {
            // For non-release updates need to revert ext.lastRelease to a release version
            ant.replaceregexp(match: "ext\\.lastRelease = '" + nextVersion + "'",
                            replace: "ext\\.lastRelease = '" + lastRelease + "'", flags: 'g', byline: true) {
                fileset(dir: '.', includes: 'build.gradle')
            }
        }
    }
}
