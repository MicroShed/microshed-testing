ext.title = "MicroShed Testing Framework :: Testcontainers extension"
description = "Extensions for using MicroShed Testing with Testcontainers for managing application and resource lifecycle"

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestCompile.extendsFrom testCompile
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.intTestImplementation
    }
}

dependencies {
    implementation 'org.testcontainers:junit-jupiter:1.19.1'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    compileOnly project(':microshed-testing-core')
    testImplementation 'org.eclipse.microprofile.rest.client:microprofile-rest-client-api:1.4.1'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.36'
    testImplementation 'org.testcontainers:mockserver:1.19.1'
    testCompileOnly project(':microshed-testing-core')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'

    integrationTestImplementation project(':microshed-testing-core')
    integrationTestImplementation project(':microshed-testing-testcontainers')
    integrationTestImplementation 'org.testcontainers:junit-jupiter:1.19.1'
    integrationTestImplementation 'org.eclipse.microprofile.rest.client:microprofile-rest-client-api:1.4.1'
    integrationTestImplementation 'org.slf4j:slf4j-log4j12:1.7.36'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    integrationTestImplementation 'org.testcontainers:mockserver:1.19.1'
}

test {
    // These system properties will trigger hollow mode to be triggered during unit tests
    systemProperty "microshed_hostname", "localhost"
    systemProperty "microshed_http_port", "9080"
}

apply from: publishScript

publishToMavenLocal.dependsOn ':microshed-testing-core:publishToMavenLocal'

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    defaultCharacterEncoding = "UTF-8"
    useJUnitPlatform()
    testLogging {
        displayGranularity 1
        showStandardStreams = true
        showStackTraces = true
        exceptionFormat = 'full'
        events 'PASSED', 'FAILED', 'SKIPPED'
    }

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest
