ext.title = "MicroShed Testing Framework :: Testcontainers extension"
description = "Extensions for using MicroShed Testing with Testcontainers for managing application and resource lifecycle"

sourceSets {
    integrationTest
}

dependencies {
    implementation project(':microshed-testing-core')
    implementation 'org.testcontainers:junit-jupiter:1.15.0'
    testImplementation 'org.eclipse.microprofile.rest.client:microprofile-rest-client-api:1.4.1'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.30'
    testImplementation 'org.testcontainers:mockserver:1.15.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'

    integrationTestImplementation project(':microshed-testing-core')
    integrationTestImplementation project(':microshed-testing-testcontainers')
    integrationTestImplementation 'org.testcontainers:junit-jupiter:1.15.0'
    integrationTestImplementation 'org.eclipse.microprofile.rest.client:microprofile-rest-client-api:1.4.1'
    integrationTestImplementation 'org.slf4j:slf4j-log4j12:1.7.30'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    integrationTestImplementation 'org.testcontainers:mockserver:1.15.0'
}

test {
  // These system properties will trigger hollow mode to be triggered during unit tests
  systemProperty "microshed_hostname", "localhost"
  systemProperty "microshed_http_port", "9080"
}

apply from: publishScript

publishToMavenLocal.dependsOn ':microshed-testing-core:publishToMavenLocal'

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    defaultCharacterEncoding = "UTF-8"
    useJUnitPlatform()
    testLogging {
        displayGranularity 1
        showStandardStreams = true
        showStackTraces = true
        exceptionFormat = 'full'
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    shouldRunAfter test
}

check.dependsOn integrationTest
