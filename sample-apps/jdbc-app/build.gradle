plugins {
    id 'war'
}

configurations {
    postgres
}

dependencies {
  api libs.jakarta.api
  api libs.mp.api

  testImplementation project(':microshed-testing-testcontainers')

  testImplementation platform(libs.slf4j.bom)
  testImplementation libs.slf4j.log4j

  testImplementation platform(libs.tc.bom)
  testImplementation libs.tc.postgresql

  testImplementation platform(libs.junit.bom)
  testImplementation libs.bundles.junit.impl
  testRuntimeOnly    libs.bundles.junit.runtime
  
  postgres libs.postgresql.jdbc
}

task copyDriver(type: Copy) {
    from configurations.postgres
    include '**/*.jar'
    into 'src/main/liberty/config/resources'
    rename 'postgresql.*jar', 'postgresql.jar'
}

war.archiveFileName.set 'myservice.war'
test.dependsOn 'war'
test.dependsOn 'copyDriver'

// Always re-run tests on every build for the sake of this sample
// In a real project, this setting would not be desirable
test.outputs.upToDateWhen { false } 
