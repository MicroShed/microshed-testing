plugins {
    id 'war'
}

configurations {
    postgres
}

dependencies {
    api 'jakarta.platform:jakarta.jakartaee-api:10.0.0'
    api 'org.eclipse.microprofile:microprofile:6.1'

    testImplementation 'org.testcontainers:postgresql:1.20.1'
    testImplementation 'org.slf4j:slf4j-log4j12:2.0.15'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'

    testImplementation project(':microshed-testing-testcontainers')

    postgres 'org.postgresql:postgresql:42.7.3'
}

task copyDriver(type: Copy) {
    from configurations.postgres
    include '**/*.jar'
    into 'src/main/liberty/config/resources'
    rename 'postgresql.*jar', 'postgresql.jar'
}

war.archiveFileName.set 'myservice.war'
test.dependsOn 'war'
test.dependsOn 'copyDriver'

// Always re-run tests on every build for the sake of this sample
// In a real project, this setting would not be desirable
test.outputs.upToDateWhen { false } 
