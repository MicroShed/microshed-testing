plugins {
    id 'war'
}

configurations {
    postgres
}

dependencies {
    api 'jakarta.platform:jakarta.jakartaee-api:10.0.0'
    api 'org.eclipse.microprofile:microprofile:7.1'

    testImplementation 'org.testcontainers:postgresql:1.21.2'
    testImplementation 'org.slf4j:slf4j-log4j12:2.0.17'

    testImplementation platform("org.junit:junit-bom:5.13.2")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    testImplementation project(':microshed-testing-testcontainers')

    postgres 'org.postgresql:postgresql:42.7.7'
}

task copyDriver(type: Copy) {
    from configurations.postgres
    include '**/*.jar'
    into 'src/main/liberty/config/resources'
    rename 'postgresql.*jar', 'postgresql.jar'
}

war.archiveFileName.set 'myservice.war'
test.dependsOn 'war'
test.dependsOn 'copyDriver'

// Always re-run tests on every build for the sake of this sample
// In a real project, this setting would not be desirable
test.outputs.upToDateWhen { false } 
